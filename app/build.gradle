plugins {
    id "com.android.application"
    id "app.accrescent.tools.bundletool" version "0.2.4"
}

def getReleaseProperties() {
    final Properties props = new Properties()
    final File propertiesFile = file("${projectDir.getParent()}/release.properties")
    if (propertiesFile.exists()) {
        props.load(new FileInputStream(propertiesFile))
    }
    return props
}

final Properties properties = getReleaseProperties()

android {
    namespace 'io.spaceapi.community.myhackerspace'

    defaultConfig {
        applicationId "io.spaceapi.community.myhackerspace"
        minSdkVersion 26
        compileSdk 36
        targetSdkVersion 36
        versionCode 109
        versionName "2.3.1"
    }

    buildFeatures {
        buildConfig = true
    }

    signingConfigs {
        if (releaseProperties['keystoreFile'] != null) {
            release {
                storeFile file(releaseProperties['keystoreFile'])
                storePassword releaseProperties['keystorePassword']
                keyAlias releaseProperties['keyAlias']
                keyPassword releaseProperties['keyPassword']
            }
        } else {
            logger.warn('No release config found. In case you want to create a release build, please ensure that `release.properties` exists. See README.md for more information.')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (releaseProperties['keystoreFile'] != null) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    lint {
        warning 'MissingTranslation'
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.9.1"

    // SpaceAPI Library
    implementation "io.github.spaceapi-community:spaceapi-kt:0.7.0"

    // JUnit for testing
    testImplementation 'junit:junit:4.13.2'
}

bundletool {
    if (releaseProperties['keystoreFile'] != null) {
        signingConfig {
            storeFile = file(releaseProperties['keystoreFile'])
            storePassword = releaseProperties['keystorePassword']
            keyAlias = releaseProperties['keyAlias']
            keyPassword = releaseProperties['keyPassword']
        }
    }
}
